group 'org.wzstudio'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

def aspectjVersion = "1.8.9"
def springVersion = "4.3.4.RELEASE"
def jdkVersion = "1.8"
def junitVersion = "4.12"
def jacksonVersion = "2.8.5"
def log4jVersion = "2.7"
def servletApiVersion = "3.1"
def thymeleafVersion = "3.0.2.RELEASE"

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://maven.atlassian.com/3rdparty/"
    }
}

configurations {
    ajc
    aspects
    compile {
        extendsFrom aspects
    }
}

dependencies {
    compile group: 'org.jsoup', name:'jsoup', version: '1.10.1'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.6'
    compile group: 'org.joda', name: 'joda-money', version: '0.12'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    compile group: 'org.springframework', name: 'spring-jdbc', version: springVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-web', version: log4jVersion
    compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: thymeleafVersion
    compile group: 'com.zaxxer', name: 'HikariCP', version:'2.4.7'
    compile group: 'postgresql', name: 'postgresql', version: '9.4.1208-jdbc42-atlassian-hosted'
    compile group: 'org.webjars', name: 'bootstrap', version: '3.3.7'
    compile group: 'org.webjars', name: 'webjars-locator', version: '0.30'
    compile group: 'org.webjars', name: 'd3js', version: '4.2.1'
    compile group: 'org.webjars.bower', name: 'datatables', version: '1.10.12'
    compile group: 'org.webjars', name: 'chartjs', version: '2.1.3'
    compile group: 'org.aspectj', name: 'aspectjrt', version: aspectjVersion
    compile group: 'org.aspectj', name: 'aspectjweaver', version: aspectjVersion
    providedCompile group: 'javax.servlet', name:'javax.servlet-api', version: servletApiVersion
    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.springframework', name:'spring-test', version: springVersion

    ajc group: 'org.aspectj', name: 'aspectjtools', version: aspectjVersion
    aspects group: 'org.springframework', name: 'spring-aspects', version: springVersion
}

// AspectJ compile
def aspectj = { destDir, aspectPath, inPath, classpath ->
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
            classpath: configurations.ajc.asPath)

    ant.iajc(
            maxmem: "1024m", fork: "true", Xlint: "ignore",
            destDir: destDir,
            aspectPath: aspectPath,
            inpath: inPath,
            classpath: classpath,
            source: project.sourceCompatibility,
            target: project.targetCompatibility
    )
}

compileJava {
    doLast {
        aspectj project.sourceSets.main.output.classesDir.absolutePath,
                configurations.aspects.asPath,
                project.sourceSets.main.output.classesDir.absolutePath,
                project.sourceSets.main.runtimeClasspath.asPath
    }
}

compileTestJava {
    dependsOn jar

    doLast {
        aspectj project.sourceSets.test.output.classesDir.absolutePath,
                configurations.aspects.asPath + jar.archivePath,
                project.sourceSets.test.output.classesDir.absolutePath,
                project.sourceSets.test.runtimeClasspath.asPath
    }
}

// gretty
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

gretty {
    // port = 8080
    contextPath = ''
    servletContainer = 'jetty9'
    scanInterval = 5
    managedClassReload = true
    fastReload = true
}
